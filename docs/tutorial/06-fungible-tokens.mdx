---
title: 6. Fungible Tokens
---

In this tutorial, we're going to deploy, store, and transfer fungible tokens.

---

<Callout type="success">
  Open the starter code for this tutorial in the Flow Playground:
  <br />
  <a
    href="https://play.onflow.org/adc24257-ccc6-4de8-aba4-e1bc33f5f029"
    target="_blank"
  >
    https://play.onflow.org/adc24257-ccc6-4de8-aba4-e1bc33f5f029
  </a>
  <br />
  The tutorial will ask you to take various actions to interact with this code.
</Callout>
<Callout type="info">
  Instructions that require you to take action are always included in a callout
  box like this one. These highlighted actions are all that you need to do to
  get your code running, but reading the rest is necessary to understand the
  language's design.
</Callout>

## Follow Along!
Developer advocate Kim dives deep on an array of topics, building on top of the information she shared in the Hello World tutorial. Learn core concepts such as creating a fungible token smart contract by using resources, resource interfaces, and using transactions to mint and transfer tokens!

<iframe width="560" height="315" src="https://www.youtube.com/embed/DInibYmxUsc" title="YouTube video player" frameborder="0" allow="accelerometer; autoplay; clipboard-write; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>

---

Some of the most popular contract classes on blockchains today are fungible tokens.
These contracts create homogeneous tokens that can be transferred to other users and spent as currency (e.g., ERC-20 on Ethereum).

In traditional software and smart contracts, balances for each user are tracked by a central ledger, such as a dictionary:

```cadence
// DO NOT USE THIS CODE FOR YOUR PROJECT
contract LedgerToken {
    // Tracks every user's balance
    access(contract) let balances: {Address: UFix64}

    // Transfer tokens from one user to the other
    // by updating their balances in the central ledger
    pub fun transfer(from: Address, to: Address, amount: UFix64) {
        balances[from] = balances[from] - amount
        balances[to] = balances[to] + amount
    }
}
```

With Cadence, we use the new resource-oriented paradigm to implement fungible tokens and avoid using a central ledger
because there are inherent problems with using a central ledger
that are detailed in [the Fungible Tokens section below.](#fungible-tokens-an-in-depth-exploration)

### Flow Network Token

In Flow, the native network token
[(FLOW) is implemented as a normal fungible token smart contract](https://github.com/onflow/flow-core-contracts/blob/master/contracts/FlowToken.cdc)
using a smart contract similar to the one in this tutorial.

There are special transactions and hooks that allow it to be used for transaction execution fees, storage fees, and staking,
but besides that, developers and users are able to treat it and use it just like any other token in the network!

<Callout type="warning">
  It is important to remember that while this tutorial implements a working
  fungible token, it has been simplified for educational purposes and is not
  what any project should use in production. See the
  <a href="https://github.com/onflow/flow-ft" target="_blank">Flow Fungible Token standard</a>
  for the standard interface and example implementation.
</Callout>

We're going to take you through these steps to get comfortable with the fungible token:

1. Deploy the fungible token contract to account `0x01`
2. Create a fungible token object and store it in your account storage.
3. Create a reference to your tokens that others can use to send you tokens.
4. Set up another account the same way.
5. Transfer tokens from one account to another.
6. Use a script to read the accounts' balances.

**Before proceeding with this tutorial**, we recommend following the instructions in [Getting Started](01-first-steps)
and [Hello, World!](02-hello-world) to learn the basics of the language and the playground.

# Fungible Tokens on the Flow Emulator

---

<Callout type="info">
  First, you'll need to follow this link to open a playground session with the
  Fungible Token contracts, transactions, and scripts pre-loaded:{" "}
  <a
    href="https://play.onflow.org/adc24257-ccc6-4de8-aba4-e1bc33f5f029"
    target="_blank"
  >
    https://play.onflow.org/adc24257-ccc6-4de8-aba4-e1bc33f5f029
  </a>
</Callout>
<Callout type="info">

Open the account `0x01` tab to see the file named
`ExampleToken.cdc`. `ExampleToken.cdc` should contain the full code for the
fungible token, which provides the core functionality to store fungible tokens
in your account and transfer to and accept tokens from other users.

</Callout>

The concepts involved in implementing a fungible token in Cadence can be unfamiliar at first.
For an in-depth explanation of this functionality and code, continue reading the next section.

Or, if you'd like to go immediately into deploying it and using it in the playground,
you can skip to the [Interacting with the Fungible Token](#interacting-with-the-fungible-token-in-the-flow-playground) section of this tutorial.

# Fungible Tokens: An In-Depth Exploration

---

How Flow implements fungible tokens is different from other programming languages. As a result:

- Ownership is decentralized and does not rely on a central ledger
- Bugs and exploits present less risk for users and less opportunity for attackers
- There is no risk of integer underflow or overflow
- Assets cannot be duplicated, and it is very hard for them to be lost, stolen, or destroyed
- Code can be composable
- Rules can be immutable
- Code is not unintentionally made public

## Decentralizing Ownership

---

Instead of using a central ledger system, Flow ties ownership to each account via a new paradigm for asset ownership.
The example below showcases how Solidity (the smart contract language for the Ethereum Blockchain, among others)
implements fungible tokens, with only the code for storage and transferring tokens shown for brevity.

```solidity:title=ERC20.sol
contract ERC20 {
    // Maps user addresses to balances, similar to a dictionary in Cadence
    mapping (address => uint256) private _balances;

    function _transfer(address sender, address recipient, uint256 amount) {
        // ensure the sender has a valid balance
        require(_balances[sender] >= amount);

        // subtract the amount from the senders ledger balance
        _balances[sender] = _balances[sender] - amount;

        // add the amount to the recipient’s ledger balance
        _balances[recipient] = _balances[recipient] + amount
    }
}
```

As you can see, Solidity uses a central ledger system for its fungible tokens. There is one contract that manages the state of the tokens
and every time that a user wants to do anything with their tokens, they have to interact with the central ERC20 contract,
calling its functions to update their balance. This contract handles access control for all functionality, implements all of its own correctness checks,
and enforces rules for all of its users.

Instead of using a central ledger system, Flow utilizes a few different concepts
to provide better safety, security, and clarity for smart contract developers and users.
In this section, we'll show how Flow's resources, interfaces, and other features are employed via a fungible token example.

## Intuiting Ownership with Resources

---

An important concept in Cadence is **Resources**, which are linear types.
A resource is a composite type (like a struct) that has its own defined fields and functions.
The difference is that resource objects have special rules that keep them from being copied or lost.
Resources are a new paradigm for asset ownership. Instead of representing token ownership in a central ledger smart contract,
each account owns its own resource object in its account storage that records the number of tokens they own.
This way, when users want to transact with each other, they can do so peer-to-peer without having to interact with a central token contract.
To transfer tokens to each other, they call a `transfer` function (or something equivalent)
on their own resource object and other users' resources, instead of a central `transfer` function.

This approach simplifies access control because instead of a central contract having to check the sender of a function call,
most function calls happen on resource objects stored in users' account,
and each user controls who is able to call the functions on resources in their account.
This concept, called Capability-based security, will be explained more in a later section.

This approach also helps protect against potential bugs. In a Solidity contract with all the logic and state contained in a central contract,
an exploit is likely to affect all users who are involved in the contract.

In Cadence, if there is a bug in the resource logic, an attacker would have to exploit the bug in each token holder's account individually,
which is much more complicated and time-consuming than it is in a central ledger system.

Below is an example of a resource for a fungible token vault. Every user who owns these tokens would have this resource stored in their account.
It is important to remember that each account stores only a copy of the `Vault` resource, and not a copy of the entire `ExampleToken` contract.
The `ExampleToken` contract only needs to be stored in the initial account that manages the token definitions.

```cadence:title=Token.cdc
pub resource Vault: Provider, Receiver {

    // Balance of a user's Vault
    // we use unsigned fixed point numbers for balances
    // because they can represent decimals and do not allow negative values
    pub var balance: UFix64

    init(balance: UFix64) {
        self.balance = balance
    }

    pub fun withdraw(amount: UFix64): @Vault {
        self.balance = self.balance - amount
        return <-create Vault(balance: amount)
    }

    pub fun deposit(from: @Vault) {
        self.balance = self.balance + from.balance
        destroy from
    }
}
```

This piece of code is for educational purposes and is not comprehensive. However, it still showcases how a resource for a token works.

### Token Balances and Initialization

Each token resource object has a balance and associated functions (e.g., `deposit`, `withdraw`, etc).
When a user wants to use these tokens, they instantiate a zero-balance copy of this resource in their account storage.
The language requires that the initialization function `init`, which is only run once, must initialize all member variables.

```cadence
// Balance of a user's Vault
// we use unsigned fixed-point integers for balances because they do not require the
// concept of a negative number and allow for more clear precision
pub var balance: UFix64

init(balance: UFix64) {
    self.balance = balance
}
```

If you remove the `init` function from your `ExampleToken` contract, it will cause an error because
the balance field is no longer initialized.

### Deposit

Then, the deposit function is available for any account to transfer tokens to.

```cadence
pub fun deposit(from: @Vault) {
    self.balance = self.balance + from.balance
    destroy from
}
```

### Transferring Tokens

When an account wants to send tokens to a different account, the sending account calls their own withdraw function first,
which subtracts tokens from their resource’s balance and temporarily creates a new resource object that holds this balance.
The sending account then calls the recipient account’s deposit function, which literally moves the resource instance to the other account,
adds it to their balance, and then destroys the used resource.
The resource needs to be destroyed because Cadence enforces strict rules around resource interactions.
A resource can never be left hanging in a piece of code. It either needs to be explicitly destroyed or stored in an account's storage.

When interacting with resources, you use the `@` symbol to specify the type, and a special “move operator” `<-`
when moving the resource, such as assigning the resource, when passing it as an argument to a function, or when returning it from a function.

```cadence
pub fun withdraw(amount: UInt64): @Vault {
```

This `@` symbol is required when specifying a resource **type** for a field, an argument, or a return value.
The move operator `<-` makes it clear that when a resource is used in an **assignment**, parameter, or return value,
it is moved to a new location and the old location is invalidated. This ensures that the resource only ever exists in one location at a time.

If a resource is moved out of an account's storage, it either needs to be moved to an account’s storage or explicitly destroyed.

```cadence
destroy from
```

This rule ensures that resources, which often represent real value, do not get lost because of a coding error.

You’ll notice that the arithmetic operations aren't explicitly protected against overflow or underflow.

```cadence
self.balance = self.balance - amount
```

In Solidity, this could be a risk for integer overflow or underflow, but Cadence has built-in overflow and underflow protection, so it is not a risk.
We are also using unsigned numbers in this example, so as mentioned earlier, the vault`s balance cannot go below 0.

Additionally, the requirement that an account contains a copy of the token’s resource type in its storage
ensures that funds cannot be lost by being sent to the wrong address.

If an address doesn’t have the correct resource type imported, the transaction will revert, ensuring that transactions sent to the wrong address are not lost.

**Important note: This protection is not in place for the Flow network currency,**
**because every Flow account is initialized with a default Flow Token Vault**
**in order to pay for [storage fees and transaction fees](/flow-token/concepts#fees).**

### Function Parameters

The line in `withdraw` that creates a new `Vault` has the parameter name `balance` specified in the function call.

```cadence
return <-create Vault(balance: amount)
```

This is another feature that Cadence uses to improve the clarity of code.
All function calls are required to specify the names of the arguments they are sending
unless the developer has specifically overridden the requirement in the funtion declaration.

## Interacting with the Fungible Token in the Flow Playground

Now that you have read about how the Fungible Token works,
we can deploy a basic version of it to your account and send some transactions to interact with it.

<Callout type="info">

Make sure that you have opened the Fungible Token templates in the playground
by following the link at the top of this page. You should have Account `0x01`
open and should see the code below.

</Callout>

```cadence
// BasicToken.cdc
//
// The BasicToken contract is a sample implementation of a fungible token on Flow.
//
// Fungible tokens behave like everyday currencies -- they can be minted, transferred or
// traded for digital goods.
//
// This is a basic implementation of a Fungible Token and is NOT meant to be used in production
// See the Flow Fungible Token standard for real examples: https://github.com/onflow/flow-ft

pub contract BasicToken {

    // Vault
    //
    // Each user stores an instance of only the Vault in their storage
    // The functions in the Vault are governed by the pre and post conditions
    // in the interfaces when they are called.
    // The checks happen at runtime whenever a function is called.
    //
    // Resources can only be created in the context of the contract that they
    // are defined in, so there is no way for a malicious user to create Vaults
    // out of thin air. A special Minter resource or constructor function needs to be defined to mint
    // new tokens.
    //
    pub resource Vault {

		// keeps track of the total balance of the account's tokens
        pub var balance: UFix64

        // initialize the balance at resource creation time
        init(balance: UFix64) {
            self.balance = balance
        }

        // withdraw
        //
        // Function that takes an integer amount as an argument
        // and withdraws that amount from the Vault.
        //
        // It creates a new temporary Vault that is used to hold
        // the money that is being transferred. It returns the newly
        // created Vault to the context that called so it can be deposited
        // elsewhere.
        //
        pub fun withdraw(amount: UFix64): @Vault {
            self.balance = self.balance - amount
            return <-create Vault(balance: amount)
        }

        // deposit
        //
        // Function that takes a Vault object as an argument and adds
        // its balance to the balance of the owners Vault.
        //
        // It is allowed to destroy the sent Vault because the Vault
        // was a temporary holder of the tokens. The Vault's balance has
        // been consumed and therefore can be destroyed.
        pub fun deposit(from: @Vault) {
            self.balance = self.balance + from.balance
            destroy from
        }
    }

    // createVault
    //
    // Function that creates a new Vault with an initial balance
    // and returns it to the calling context. A user must call this function
    // and store the returned Vault in their storage in order to allow their
    // account to be able to receive deposits of this token type.
    //
    pub fun createVault(): @Vault {
        return <-create Vault(balance: 30.0)
    }

    // The init function for the contract. All fields in the contract must
    // be initialized at deployment. This is just an example of what
    // an implementation could do in the init function. The numbers are arbitrary.
    init() {
        // create the Vault with the initial balance and put it in storage
        // account.save saves an object to the specified `to` path
        // The path is a literal path that consists of a domain and identifier
        // The domain must be `storage`, `private`, or `public`
        // the identifier can be any name
        let vault <- self.createVault()
        self.account.save(<-vault, to: /storage/CadenceFungibleTokenTutorialVault)
    }
}
```

<Callout type="info">

Click the `Deploy` button at the top right of the editor to deploy the code.

</Callout>

<img src="https://storage.googleapis.com/flow-resources/documentation-assets/cadence-tuts/playground-deploy.png" />

This deployment stores the contract for the basic fungible token
in your active account (account `0x01`) so that it can be imported into transactions.

A contract's `init` function runs at contract creation, and never again afterwards.
In our example, this function stores an instance of the `Vault` object with an initial balance of 30.

```cadence
// create the Vault with the initial balance and put it in storage
// account.save saves an object to the specified `to` path
// The path is a literal path that consists of a domain and identifier
// The domain must be `storage`, `private`, or `public`
// the identifier can be any name
let vault <- self.createVault()
self.account.save(<-vault, to: /storage/CadenceFungibleTokenTutorialVault)
```

This line saves the new `@Vault` object to storage.
Account storage is indexed with paths, which consist of a domain and identifier. `/domain/identifier`. Only three domains are allowed for paths:

- `storage`: The place where all objects are stored. Only accessible by the owner of the account.
- `private`: Stores links, otherwise known as capabilities, to objects in storage. Only accessible by the owner of the account
- `public`: Stores links to objects in storage: Accessible by anyone in the network.

Contracts have access to the private `AuthAccount` object of the account it is deployed to, using `self.account`.
This object has methods that can modify storage in many ways.
See the [account](/cadence/language/accounts) documentation for a list of all the methods it can call.

In this line, we call the `save` method to store an object in storage.
The first argument is the value to store, and the second argument is the path where the value is being stored.
For `save` the path has to be in the `/storage/` domain.

You should also see that the `ExampleToken.Vault` and `ExampleToken.VaultMinter` resource objects are stored in the account storage.
This will be shown in the Resources box at the bottom of the screen.

You are now ready to run transactions that use the fungible tokens!

### Perform a Basic Transfer

As we talked about above, a token transfer with resources is not a simple update to a ledger.
In Cadence, you have to first withdraw tokens from your vault, then deposit them to the vault
that you want to transfer to. We'll start a simple transaction that withdraws tokens from a vault
and deposits them back into the same vault.

<Callout type="info">

Open the transaction named `Basic Transfer`. <br/>
`Basic Transfer` should contain the following code for withdrawing and depositing with a stored Vault:

</Callout>

```cadence:title=Basic Transfer.cdc
// Basic Transfer

import BasicToken from 0x01

// This transaction is used to withdraw and deposit tokens with a Vault

transaction {

  prepare(acct: AuthAccount) {
    // withdraw tokens from your vault by borrowing a reference to it
    // and calling the withdraw function with that reference
    let vaultRef = acct.borrow<&BasicToken.Vault>(from: /storage/CadenceFungibleTokenTutorialVault)
        ?? panic("Could not borrow a reference to the owner's vault")

    let temporaryVault <- vaultRef.withdraw(amount: 10.0)

    // deposit your tokens to the Vault
    vaultRef.deposit(from: <-temporaryVault)

    log("Withdraw/Deposit succeeded!")
  }
}
```

<Callout type="info">
  Select account `0x01` as the only signer. <br />
  Click the `Send` button to submit the transaction. <br />
  This transaction withdraws tokens from the main vault and deposits them back
  to it.
</Callout>

This transaction is a basic example of a transfer within an account.
It withdraws tokens from the main vault and deposits back to the main vault.
It is simply to illustrate the basic functionality of how transfers work.

You'll see in this transaction that
you can borrow a reference directly from an object in storage.

```cadence
// Borrow a reference to the stored, private Vault resource
let vaultRef = acct.borrow<&BasicToken.Vault>(from: /storage/CadenceFungibleTokenTutorialVault)
    ?? panic("Could not borrow a reference to the owner's vault")
```

This allows you to efficiently access objects in storage without having to load them,
which is a much more costly interaction.

In production code, you'll likely be transferring tokens to other accounts.
Capabilities allow us to accomplish this safely.

## Ensuring Security in Public: Capability Security

---

Another important feature in Cadence is its utilization of [**Capability Security.**](/cadence/language/capability-based-access-control)
This feature ensures that while the withdraw function is declared public on the resource,
no one except the intended user and those they approve of can withdraw tokens from their vault.

Cadence's security model ensures that objects stored in an account's storage can only be accessed by the account that owns them.
If a user wants to give another user access to their stored objects, they can link a public capability,
which is like an "API" that allows others to call specified functions on their objects.

An account only has access to the fields and methods of an object in a different account if they hold a capability to that object
that explicitly allows them to access those fields and methods.
Only the owner of an object can create a capability for it.
Therefore, when a user creates a Vault in their account, they only publish a capability
that exposes the deposit function and the balance field.

The withdraw function can remain hidden as a function that only the owner can call.

This removes the need to check the address of the account that made the function call
(`msg.sender` in Ethereum) for access control purposes, because this functionality is handled by the protocol and type checker.
If you aren't the owner of an object or don't have a valid reference to it that was created by the owner, you cannot access the object at all!

### Using Interfaces to Secure Implementations

---

The next important concept in Cadence is design-by-contract,
which uses preconditions and postconditions to document and programmatically assert the change in state caused by a piece of a program.
These conditions are specified in interfaces that enforce rules about how types are defined and behave.
They can be stored on-chain in an immutable fashion so that certain pieces of code can import and implement them to ensure that they meet certain standards.

Here is an example of how interfaces for the `Vault` resource we defined above would look.

```cadence:title=Interfaces.cdc
// Interface that enforces the requirements for withdrawing
// tokens from the implementing type
//
pub resource interface Provider {
    pub fun withdraw(amount: UFix64): @Vault {
        post {
            result.balance == amount:
                "Withdrawal amount must be the same as the balance of the withdrawn Vault"
        }
    }
}
// Interface that enforces the requirements for depositing
// tokens into the implementing type
//
pub resource interface Receiver {

    // There aren't any meaningful requirements for only a deposit function
    // but this still shows that the deposit function is required in an implementation.
    pub fun deposit(from: @Vault)
}

// Balance
//
// Interface that specifies a public `balance` field for the vault
//
pub resource interface Balance {
    pub var balance: UFix64
}
```

In our example, the `Vault` resource will implement all three of these interfaces.
The interfaces ensure that specific fields and functions are present in the resource implementation
and that the function arguments, fields of the resource, and any return value are in a valid state before and/or after execution.
These interfaces can be stored on-chain and imported into other contracts or resources
so that these requirements are enforced by an immutable source of truth that is not susceptible to human error.

You can also see that functions and fields have the `pub` keyword next to them.
We have explicitly defined these fields as public because all fields and functions in Cadence are private by default,
meaning that the local scope can only access them. Users have to make parts of their owned types explicitly public.
This helps prevent types from having unintentionally public code.

This does bring up an important security consideration though.
While we have made all our fields and functions public here, it is actually recommended to default to making
fields private unless it is explicitly needed to be public.

<Callout type="warning">
  This is especially important for array and dictionary types,
  which can have their contents maliciously mutated if they are made public.
  This is one of THE MOST COMMON security mistakes that Cadence developers make,
  so it is vitally important to be aware of this.

  See the [Cadence Best Practices document](/cadence/anti-patterns#array-or-dictionary-fields-should-be-private) for more details.
</Callout>

## Adding Interfaces to Our Fungible Token

Now, we are going to add these interfaces to our Fungible token along with a minter resource.

Open account `0x02` in the playground. You should see the `ExampleToken` contract.
In addition to everything that is in the `BasicToken` contract,
we have also added the `Provider`, `Receiver`, and `Balance` interfaces described above.

Now that our `ExampleToken.Vault` type has declared that it implements these interfaces on line 93,
it is required to have their fields and functions, and their pre and post-conditions will also
be evaluated every time their respective functions are called.
We can also use these interfaces to create restricted capabilities, described in the next section.

Additionally, `ExampleToken` changes `createVault()` to `createEmptyVault()`
so that token minting is restricted to the newly added `VaultMinter` resource.
This illustrates another powerful feature of Cadence resources.
Instead of the contract maintaining a list of minter addresses,
accounts that the owner wants to be minters can be giving a special resource
that directly gives them the authority to mint tokens.
This method for authorization can be used in many different ways
and further decentralizes the control of the contract.

We also store the `VaultMinter` object to `/storage/`
in the `init()` function in the same way as the vault, but in a different storage path:

```cadence
self.account.save(<-create VaultMinter(), to: /storage/CadenceFungibleTokenTutorialMinter)
```

Now is an important time to remind you that account storage not namespaced by contract,
meaning that path names could potentially conflict. This is why it is important to
choose unique names for your paths like we have done here so there is a very low chance
of them conflicting with other projects paths.

## Create, Store, and Publish Capabilities and References to a Vault

---

Capabilities are like pointers in other languages. They are a link to an object in an account's storage
and can be used to read fields or call functions on the object they reference. They cannot move or modify the object directly.

There are many different situations in which you would create a capability to your fungible token vault.
You might want a simple way to call methods on your `Vault` from anywhere in a transaction.
You could also send a capability that only exposes withdraw function in your `Vault` so that others can transfer tokens for you.
You could also have one that only exposes the `Balance` interface, so that others can check how many tokens you own.
There could also be a function that takes a capability to a `Vault` as an argument, borrows a reference to the capability,
makes a single function call on the reference, then finishes and destroys the reference.

We already use this pattern in the `VaultMinter` resource in the `mintTokens` function, shown here:

```cadence
// Function that mints new tokens and deposits into an account's vault
// using their `Receiver` capability.
// We say `&AnyResource{Receiver}` to say that the recipient can be any resource
// as long as it implements the ExampleToken.Receiver interface
pub fun mintTokens(amount: UFix64, recipient: Capability<&AnyResource{Receiver}>) {
    let recipientRef = recipient.borrow()
        ?? panic("Could not borrow a receiver reference to the vault")

    ExampleToken.totalSupply = ExampleToken.totalSupply + UFix64(amount)
    recipientRef.deposit(from: <-create Vault(balance: amount))
}
```

The function takes a capability as an argument.
This syntax might be unclear to you:

```cadence
recipient: Capability<&AnyResource{Receiver}>
```

This means that `recipient` has to be a Capability that is restricted to the type contained in `<>`.
The type outside of the curly braces `{}` has to be a concrete type and the type in the curly braces
has to be an interface type. Here we are saying that the type
can be any resource that implements the `ExampleToken.Receiver` interface.
If that is true, this function borrows a reference from this capability
and uses the reference to call the `deposit` function of that resource because we know that
the `deposit` function will be there since it is in the `ExampleToken.Receiver` interface.

Let's create capabilities to your `Vault` so that a separate account can send tokens to you.

<Callout type="info">

Open the transaction named `Create Link`. <br/>
`Create Link` should contain the following code for creating a reference to the stored Vault:

</Callout>

```cadence:title=Create Link.cdc
// Create Link

import ExampleToken from 0x02

// This transaction creates a capability
// that is linked to the account's token vault.
// The capability is restricted to the fields in the `Receiver` interface,
// so it can only be used to deposit funds into the account.
transaction {
  prepare(acct: AuthAccount) {

    // Create a link to the Vault in storage that is restricted to the
    // fields and functions in `Receiver` and `Balance` interfaces,
    // this only exposes the balance field
    // and deposit function of the underlying vault.
    //
    acct.link<&ExampleToken.Vault{ExampleToken.Receiver, ExampleToken.Balance}>(/public/CadenceFungibleTokenTutorialReceiver, target: /storage/CadenceFungibleTokenTutorialVault)

    log("Public Receiver reference created!")
  }

  post {
    // Check that the capabilities were created correctly
    // by getting the public capability and checking
    // that it points to a valid `Vault` object
    // that implements the `Receiver` interface
    getAccount(0x01).getCapability<&ExampleToken.Vault{ExampleToken.Receiver}>(/public/CadenceFungibleTokenTutorialReceiver)
                    .check():
                    "Vault Receiver Reference was not created correctly"
    }
}
```

In order to use a capability, we have to first create a link to that object in storage.
A reference can then be created from a capability, and references cannot be stored.
They need to be lost at the end of a transaction execution.
This restriction is to prevent reentrancy attacks which are attacks where a malicious user calls into the same function over and over again
before the original execution has finished. Only allowing one reference at a time for an object prevents these attacks for objects in storage.

To create a capability, we use the `link` function.

```cadence
// Create a link to the Vault in storage that is restricted to the
// fields and functions in `Receiver` and `Balance` interfaces,
// this only exposes the balance field
// and deposit function of the underlying vault.
//
acct.link<&ExampleToken.Vault{ExampleToken.Receiver, ExampleToken.Balance}>.
(/public/CadenceFungibleTokenTutorialReceiver, target: /storage/CadenceFungibleTokenTutorialVault)
```

`link` creates a new capability that is kept at the path in the first argument, targeting the `target` in the second argument.
The type restriction for the link is specified in the `<>`. We use `&ExampleToken.Vault{ExampleToken.Receiver, ExampleToken.Balance}`
to say that the link can be any resource as long as it implements and is cast as the Receiver interface.
This is the common format for describing references.
You first have a `&` followed by the concrete type, then the interface in curly braces to ensure that
it is a reference that implements that interface and only includes the fields specified in that interface.

We put the capability in `/public/CadenceFungibleTokenTutorialReceiver` because we want it to be publicly accessible.
The `public` domain of an account is accessible to anyone in the network via an account's `PublicAccount` object, which is fetched by using the `getAccount(address)` function.

Next is the `post` phase of the transaction.

```cadence
post {
// Check that the capabilities were created correctly
// by getting the public capability and checking
// that it points to a valid `Vault` object
// that implements the `Receiver` interface
getAccount(0x01).getCapability(/public/CadenceFungibleTokenTutorialReceiver)
                .check<&ExampleToken.Vault{ExampleToken.Receiver}>():
                "Vault Receiver Reference was not created correctly"
}
```

The `post` phase is for ensuring that certain conditions are met after the transaction has been executed.
Here, we are getting the capability from its public path and calling its `check` function to ensure
that the capability contains a valid link to a valid object in storage that is the specified type.

<Callout type="info">

Select account `0x02` as the only signer. <br/>
Click the `Send` button to submit the transaction. <br/>
This transaction creates a new public reference to your `Vault` and checks that it was created correctly.

</Callout>

## Transfer Tokens to Another User

---

Now, we are going to run a transaction that sends 10 tokens to account `0x03`.
We will do this by calling the `withdraw` function on account `0x02`'s Vault,
which creates a temporary Vault object for moving the tokens,
then deposits those tokens into account `0x03`'s vault by calling the `deposit` function on their vault.

Here we encounter another safety feature that Cadence introduces. Owning tokens requires you to have a `Vault` object stored in your account,
so if anyone tries to send tokens to an account who isn't prepared to receive them, the transaction will fail.
This way, Cadence protects the user if they accidentally enter the account address incorrectly when sending tokens.

Account `0x03` has not been set up to receive tokens, so we will do that now:

<Callout type="info">

Open the transaction `Setup Account`.<br/>
Select account `0x03` as the only signer. <br/>
Click the `Send` button to set up account `0x03` so that it can receive tokens.

</Callout>

```cadence:title=Setup Account.cdc
// Setup Account

import ExampleToken from 0x02

// This transaction configures an account to store and receive tokens defined by
// the ExampleToken contract.
transaction {
	prepare(acct: AuthAccount) {
		// Create a new empty Vault object
		let vaultA <- ExampleToken.createEmptyVault()

		// Store the vault in the account storage
		acct.save<@ExampleToken.Vault>(<-vaultA, to: /storage/CadenceFungibleTokenTutorialVault)

        log("Empty Vault stored")

        // Create a public Receiver capability to the Vault
		let ReceiverRef = acct.link<&ExampleToken.Vault{ExampleToken.Receiver, ExampleToken.Balance}>(/public/CadenceFungibleTokenTutorialReceiver, target: /storage/CadenceFungibleTokenTutorialVault)

        log("References created")
	}

    post {
        // Check that the capabilities were created correctly
        getAccount(0x03).getCapability<&ExampleToken.Vault{ExampleToken.Receiver}>(/public/CadenceFungibleTokenTutorialReceiver)
                        .check():
                        "Vault Receiver Reference was not created correctly"
    }
}
```

Here we perform the same actions that account `0x02` did to set up its `Vault`, but all in one transaction.
Account `0x03` is ready to start building its fortune! As you can see, when we created the Vault for account `0x03`,
we had to create one with a balance of zero by calling the `createEmptyVault()` function.
Resource creation is restricted to the contract where it is defined, so in this way, the Fungible Token smart contract can ensure that
nobody is able to create new tokens out of thin air.

As part of the initial deployment process for the ExampleToken contract, account `0x02` created a `VaultMinter` object.
By using this object, the account that owns it can mint new tokens.
Right now, account `0x02` owns it, so it has sole power to mint new tokens.
We could have had a `mintTokens` function defined in the contract,
but then we would have to check the sender of the function call to make sure that they are authorized,
which is not the recommended way to perform access control.

As we explained before, the resource model plus capability security handles this access control for us as a built in language construct
instead of having to be defined in the code. If account `0x02` wanted to authorize another account to mint tokens,
they could either move the `VaultMinter` object to the other account, or give the other account a private capability to the single `VaultMinter`.
Or, if they didn't want minting to be possible after deployment, they would simply mint all the tokens at contract initialization
and not even include the `VaultMinter` in the contract.

In the next transaction, account `0x02` will mint 30 new tokens and deposit them into account `0x03`'s newly created Vault.

<Callout type="info">

Open the `Mint Tokens` transaction.
Select only account `0x02` as a signer and send `Mint Tokens` to mint 30 tokens for account `0x03`.

<br />

`Mint Tokens` should contain the code below.

</Callout>

```cadence:title=Mint Tokens.cdc
// Mint Tokens

import ExampleToken from 0x02

// This transaction mints tokens and deposits them into account 3's vault
transaction {

    // Local variable for storing the reference to the minter resource
    let mintingRef: &ExampleToken.VaultMinter

    // Local variable for storing the reference to the Vault of
    // the account that will receive the newly minted tokens
    var receiver: Capability<&ExampleToken.Vault{ExampleToken.Receiver}>

	prepare(acct: AuthAccount) {
        // Borrow a reference to the stored, private minter resource
        self.mintingRef = acct.borrow<&ExampleToken.VaultMinter>(from: /storage/CadenceFungibleTokenTutorialMinter)
            ?? panic("Could not borrow a reference to the minter")

        // Get the public account object for account 0x03
        let recipient = getAccount(0x03)

        // Get their public receiver capability
        self.receiver = recipient.getCapability<&ExampleToken.Vault{ExampleToken.Receiver}>
(/public/CadenceFungibleTokenTutorialReceiver)

	}

    execute {
        // Mint 30 tokens and deposit them into the recipient's Vault
        self.mintingRef.mintTokens(amount: 30.0, recipient: self.receiver)

        log("30 tokens minted and deposited to account 0x03")
    }
}


```

This is the first example of a transaction where we utilize local transaction variables that span different stages in the transaction.
We declare the `mintingRef` and `receiverRef` variables outside of the prepare stage but must initialize them in prepare.
We can then use them in later stages in the transaction.

Then we borrow a refernce to the `VaultMinter`. We specify the borrow as a `VaultMinter` reference
and have the reference point to `/storage/CadenceFungibleTokenTutorialMinter`.
The reference is borrowed as an optional so we use the nil-coalescing operator (`??`) to make sure the value isn't nil.
If the value is nil, the transaction will execute the code after the `??`. The code is a panic, so it will revert and print the error message.

You can use the `getAccount()` built-in function to get any account's public account object.
The public account object lets you get capabilities from the `public` domain of an account, where public capabilities are stored.

We use the `getCapability` function to get the public capability from a public path,
then use the `borrow` function on the capability to get the reference from it, typed as a `ExampleToken.Vault{ExampleToken.Receiver}`.

```cadence
// Get the public receiver capability
let cap = recipient.getCapability(/public/CadenceFungibleTokenTutorialReceiver)

// Borrow a reference from the capability
self.receiverRef = cap.borrow<&ExampleToken.Vault{ExampleToken.Receiver}>()
        ?? panic("Could not borrow a reference to the receiver")
```

In the execute phase, we simply use the reference to mint 30 tokens and deposit them into the `Vault` of account `0x03`.

## Check Account Balances

Now, both account `0x02` and account `0x03` should have a `Vault` object in their storage that has a balance of 30 tokens.
They both should also have a `Receiver` capability stored in their `/public/` domains that links to their stored `Vault`.

<Img src="https://storage.googleapis.com/flow-resources/documentation-assets/cadence-tuts/account-balances.png" />

An account cannot receive any token type unless it is specifically configured to accept those tokens.
As a result, it is difficult to send tokens to the wrong address accidentally.
But, if you make a mistake setting up the `Vault` in the new account, you won't be able to send tokens to it.

Let's run a script to make sure we have our vaults set up correctly.

You can use scripts to access an account's public state. Scripts aren't signed by any account and cannot modify state.

In this example, we will query the balance of each account's vault. The following will print out the balance of each account in the emulator.

<Callout type="info">

Open the script named `Get Balances` in the scripts pane.<br/>
`Get Balances` should contain the following code:

</Callout>

```cadence:title=Script1.cdc
// Get Balances

import FungibleToken from 0x02

// This script reads the Vault balances of two accounts.
pub fun main() {
    // Get the accounts' public account objects
    let acct2 = getAccount(0x02)
    let acct3 = getAccount(0x03)

    // Get references to the account's receivers
    // by getting their public capability
    // and borrowing a reference from the capability
    let acct2ReceiverRef = acct2.getCapability(/public/CadenceFungibleTokenTutorialReceiver)
                            .borrow<&FungibleToken.Vault{FungibleToken.Balance}>()
                            ?? panic("Could not borrow a reference to the acct2 receiver")
    let acct3ReceiverRef = acct3.getCapability(/public/CadenceFungibleTokenTutorialReceiver)
                            .borrow<&FungibleToken.Vault{FungibleToken.Balance}>()
                            ?? panic("Could not borrow a reference to the acct3 receiver")

    // Use optional chaining to read and log balance fields
    log("Account 2 Balance")
	log(acct2ReceiverRef.balance)
    log("Account 3 Balance")
    log(acct3ReceiverRef.balance)
}

```

<Callout type="info">

Execute `Get Balances` by clicking the Execute button.<br/>
This should ensure the following:

</Callout>

- Account `0x02`'s balance is 30
- Account `0x03`'s balance is 30

If correct, you should see the following lines:

```
"Account 1 Balance"
30
"Account 2 Balance"
30
Result > "void"
```

If there is an error, this probably means that you missed a step earlier
and might need to restart from the beginning.

To restart the playground, close your current session and open the link at the top of the tutorial.

Now that we have two accounts, each with a `Vault`, we can see how they transfer tokens to each other!

<Callout type="info">

Open the transaction named `Transfer Tokens`. <br/>
Select account `0x03` as a signer and send the transaction. <br/>
`Transfer Tokens` should contain the following code for sending tokens to another user:

</Callout>

```cadence:title=Transfer Tokens.cdc
// Transfer Tokens

import ExampleToken from 0x02

// This transaction is a template for a transaction that
// could be used by anyone to send tokens to another account
// that owns a Vault
transaction {

  // Temporary Vault object that holds the balance that is being transferred
  var temporaryVault: @ExampleToken.Vault

  prepare(acct: AuthAccount) {
    // withdraw tokens from your vault by borrowing a reference to it
    // and calling the withdraw function with that reference
    let vaultRef = acct.borrow<&ExampleToken.Vault>(from: /storage/CadenceFungibleTokenTutorialVault)
        ?? panic("Could not borrow a reference to the owner's vault")

    self.temporaryVault <- vaultRef.withdraw(amount: 10.0)
  }

  execute {
    // get the recipient's public account object
    let recipient = getAccount(0x02)

    // get the recipient's Receiver reference to their Vault
    // by borrowing the reference from the public capability
    let receiverRef = recipient.getCapability(/public/CadenceFungibleTokenTutorialReceiver)
                      .borrow<&ExampleToken.Vault{ExampleToken.Receiver}>()
                      ?? panic("Could not borrow a reference to the receiver")

    // deposit your tokens to their Vault
    receiverRef.deposit(from: <-self.temporaryVault)

    log("Transfer succeeded!")
  }
}

```

In this example, the signer withdraws tokens from their `Vault`,
which creates and returns a temporary `Vault` resource object with `balance=10`
that is used for transferring the tokens. In the execute phase,
the transaction moves that resource to another user's `Vault` using their `deposit` method.
The temporary `Vault` is destroyed after its balance is added to the recipient's `Vault`.

You might be wondering why we have to use two function calls to complete a token transfer when it is possible to do it in one.
This is because of the way resources work in Cadence.
In a ledger-based model, you would just call transfer, which just updates the ledger,
but in Cadence, the location of the tokens matters,
and therefore most token transfer situations will not just be a direct account-to-account transfer.

Most of the time, tokens will be used for a different purpose first,
like purchasing something, and that requires the `Vault` to be separately sent
and verified before being deposited to the storage of an account.

Separating the two also allows us to take advantage of being able
to statically verify which parts of accounts can be modified in the `prepare` section of a transaction,
which will help users have peace of mind when getting fed transactions to sign from an app.

<Callout type="info">

Execute `Get Balances` again.

</Callout>

If correct, you should see the following lines indicating that account `0x02`'s balance is 40 and account `0x03`'s balance is 20:

```
"Account 2 Balance"
40
"Account 3 Balance"
20
Result > "void"
```

You now know how a basic fungible token is used in Cadence and Flow!

From here, you could try to extend the functionality of fungible tokens by making:

- A faucet for these tokens
- An escrow that can be deposited to (but only withdrawn when the balance reaches a certain point)
- A function to the resource that mints new tokens!

## Create a Flow Marketplace

---
Now that you have an understanding of how fungible tokens work on Flow and have a working NFT, you can learn how to create 
a marketplace that uses both fungible tokens and NFTs. Move on to the next tutorial to learn about Marketplaces in Cadence!
